plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.116'
    id 'idea'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    mavenCentral() // Luckperms
    maven { url = "https://maven.createmod.net" } // Create, Ponder, Flywheel
    maven { url = "https://maven.ithundxr.dev/snapshots" } // Registrate
    maven { url = 'https://maven.fallenbreath.me/releases' } // Conditional Mixins
    maven { url = "https://api.modrinth.com/maven" } // Grief Logger
    maven {url = "https://cursemaven.com" } // SnR Neoforge (temporary)
    maven { url = "https://maven.squiddev.cc" } // CC: Tweaked
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = project.neo_version  // NeoForge version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    runs {
        client {
            client()
        }
        server {
            server()
            programArgument '--nogui'
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// localRuntime dependency configuration
configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {
    // Create & libraries
    implementation("com.simibubi.create:create-${minecraft_version}:${create_version}:slim") { transitive = false }
    implementation("net.createmod.ponder:ponder-neoforge:$ponder_version+mc$minecraft_version")
    compileOnly("dev.engine-room.flywheel:flywheel-neoforge-api-${minecraft_version}:${flywheel_version}")
    runtimeOnly("dev.engine-room.flywheel:flywheel-neoforge-${minecraft_version}:${flywheel_version}")
    implementation("com.tterrag.registrate:Registrate:${registrate_version}")

    // Other
    jarJar(implementation("me.fallenbreath:conditional-mixin-neoforge:${conditional_mixin_version}"))
    compileOnly("maven.modrinth:open-parties-and-claims:neoforge-${minecraft_version}-${opac_version}")

    // All optional dependencies
    if (load_optional_mods.toBoolean()) {
        localRuntime("cc.tweaked:cc-tweaked-${minecraft_version}-forge:${cc_tweaked_version}")
        localRuntime("maven.modrinth:open-parties-and-claims:neoforge-${minecraft_version}-${opac_version}")
        localRuntime("maven.modrinth:create-steam-n-rails-${minecraft_version}:${snr_neoforge_version}+neoforge-mc${minecraft_version}")
        localRuntime("maven.modrinth:blocks-bogies:${blocks_and_bogies}-1.21.1")
    }
}

var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
            mod_issues             : mod_issues
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}

sourceSets.main.resources.srcDir generateModMetadata
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
